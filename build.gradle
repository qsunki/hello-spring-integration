plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id "com.diffplug.spotless" version "6.25.0"
}

bootJar.enabled = false

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-starter-integration'

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.integration:spring-integration-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
        jvmArgs '-XX:+EnableDynamicAgentLoading'
    }

    spotless {
        format 'misc', {
            target '*.gradle'
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }
        java {
            removeUnusedImports()
            googleJavaFormat().aosp().reflowLongStrings().skipJavadocFormatting()
            formatAnnotations()
        }
    }
}